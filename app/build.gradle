plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.drdisagree.iconify"
        minSdk 31
        targetSdk 34
        versionCode 16
        versionName "6.6.0"
        setProperty("archivesBaseName", "Iconify v" + versionName)
        buildConfigField "int", "MIN_SDK_VERSION", "$minSdkVersion.apiLevel"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    final def keystorePropertiesFile = rootProject.file("keystore.properties")
    def releaseSigning = signingConfigs.debug
    try (def keystorePropertiesStream = new FileInputStream(keystorePropertiesFile)) {
        final def keystoreProperties = new Properties()
        keystoreProperties.load(keystorePropertiesStream)
        releaseSigning = signingConfigs.create("release") {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    catch (ignored) {
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            crunchPngs true
            proguardFiles "proguard-android-optimize.txt", "proguard.pro", "proguard-rules.pro"
            applicationIdSuffix ".debug"
            resValue("string", "derived_app_name", "Iconify (Debug)")
            signingConfig releaseSigning
        }

        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs true
            proguardFiles "proguard-android-optimize.txt", "proguard.pro", "proguard-rules.pro"
            resValue("string", "derived_app_name", "Iconify")
            signingConfig releaseSigning
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    packagingOptions {
        jniLibs {
            excludes += ['/META-INF/*',
                         '/META-INF/versions/**',
                         '/org/bouncycastle/**',
                         '/kotlin/**',
                         '/kotlinx/**']
        }

        resources {
            excludes += ['/META-INF/*',
                         '/META-INF/versions/**',
                         '/org/bouncycastle/**',
                         '/kotlin/**',
                         '/kotlinx/**',
                         'rebel.xml',
                         '/*.txt',
                         '/*.bin',
                         '/*.json']
        }

        jniLibs.useLegacyPackaging true
    }

    namespace 'com.drdisagree.iconify'
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
    // Magisk libsu version
    def libsuVersion = '5.2.1'

    // Kotlin
    implementation 'androidx.core:core-ktx:1.12.0'

    // Data Binding
    implementation 'com.android.databinding:library:8.2.2'

    // Xposed API
    compileOnly files('libs/api-82.jar')
    compileOnly files('libs/api-82-sources.jar')

    // The core module that provides APIs to a shell
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    // Optional: APIs for creating root services. Depends on ":core"
    implementation "com.github.topjohnwu.libsu:service:${libsuVersion}"
    // Optional: Provides remote file system support
    implementation "com.github.topjohnwu.libsu:nio:${libsuVersion}"

    // Color Picker
    implementation 'com.github.PolideaInternal:ColorPicker:1.0.0'

    // Splash Screen
    implementation 'androidx.core:core-splashscreen:1.0.1'

    // Material Components
    implementation 'com.google.android.material:material:1.12.0-alpha03'

    // APK Signer
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.76'

    // Zip Util
    implementation 'net.lingala.zip4j:zip4j:2.11.5'

    // Remote Preference
    implementation 'com.crossbowffs.remotepreferences:remotepreferences:0.8'

    // Flexbox
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    //noinspection KaptUsageInsteadOfKsp
    kapt 'com.github.bumptech.glide:compiler:4.16.0'

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0"

    // ViewPager2
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    // Circle Indicator
    implementation 'me.relex:circleindicator:2.1.6'

    // Lottie Animation
    implementation 'com.airbnb.android:lottie:6.1.0'

    // HTML Parser
    implementation 'org.jsoup:jsoup:1.17.2'

    // Collapsing Toolbar with subtitle
    implementation "com.hendraanggrian.material:collapsingtoolbarlayout-subtitle:1.5.0"

    // Navigation Component
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.6'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.6'

    // Concurrency
    implementation 'androidx.work:work-runtime:2.9.0'
    implementation "androidx.concurrent:concurrent-futures:1.1.0"
    implementation 'com.google.guava:guava:29.0-jre'

    // Event Bus
    implementation 'org.greenrobot:eventbus:3.3.1'

    // Dots Indicator
    implementation 'com.tbuonomo:dotsindicator:4.3'

    // Misc
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.work:work-runtime:2.9.0'
    implementation 'androidx.work:work-runtime-ktx:2.9.0'
    implementation 'org.slf4j:slf4j-api:2.0.9'

    // Test Implementation
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

tasks.register('printVersionName') {
    println android.defaultConfig.versionName.replaceAll("-(Stable|Beta)", "")
}
